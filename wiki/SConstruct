# -*- python -*-

import glob
import os

def wiki_add_depends(target, source, env):
    env.Depends(target,
                ["./wiki2html.py", 
                 "template.xml"])
    return (target, source)

wiki_bld = Builder(action = './wiki2html.py -o build $SOURCE',
                   emitter = wiki_add_depends)

env = Environment(BUILDERS = {'Wiki' : wiki_bld})

Mkdir("build")

env.Command("build/default.css", "default.css",
            Copy("$TARGET", "$SOURCE"))

output_files = []
for in_filename in glob.glob('*.wiki'):
    (filename, ext) = os.path.splitext(in_filename)
    out_filename = "build/" + filename + ".html"
    output_files.append(out_filename)
    
    env.Wiki(out_filename, in_filename)

sys_env = Environment(ENV = os.environ)
upload_cmd = sys_env.Alias('upload', [], 
                           "rsync -vrPcLC  build/ grumbel@shell.berlios.de:/home/groups/windstille/htdocs/wiki")

sys_env.AlwaysBuild(upload_cmd)
Depends(upload_cmd, output_files)

# EOF #
